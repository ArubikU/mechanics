plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

allprojects{

    group = 'dev.wuason'
    version = '1.0.1.11a'

    repositories {
        mavenCentral()
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url 'https://oss.sonatype.org/content/groups/public/' }
        maven { url 'https://nexus.phoenixdevt.fr/repository/maven-public/' }
        maven { url 'https://mvn.lumine.io/repository/maven-public/' }
        maven { url 'https://repo.codemc.org/repository/maven-public/' }
        maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://jitpack.io' }
        maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
        maven { url 'https://maven.enginehub.org/repo/' }
        maven { url 'https://nexus.frengor.com/repository/public/' }
        mavenCentral()
    }
}

dependencies{
    implementation project(path: ':core')
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'
}
shadowJar {
    relocate("dev.jorel.commandapi", "dev.wuason.libs.commandapi")
    relocate("net.kyori", "dev.wuason.libs.kyori")
    relocate("de.tr7zw.changeme.nbtapi","dev.wuason.libs.nbtapi")
    relocate("io.th0rgal.protectionlib", "dev.wuason.libs.protectionlib")
    relocate("dev.wuason.mechanics.invmechanic", "dev.wuason.libs.invmechaniclib")
    relocate("bsh", "dev.wuason.libs.bsh")
    relocate("dev.dejvokep.boostedyaml", "dev.wuason.libs.boostedyaml")
    relocate("org.apache.commons.lang3", "dev.wuason.libs.apache.lang3")
    destinationDirectory.set(file("$rootDir/plugin"))
    archiveClassifier.set("")
    archiveFileName.set("Mechanics-" + project.version + ".jar")
}
